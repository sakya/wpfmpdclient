; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{FF7AD932-3C06-4925-B3F4-DEF0B3608339}
AppName=WpfMpdClient
AppVerName=WpfMpdClient
DefaultDirName={pf}\WpfMpdClient
DefaultGroupName=WpfMpdClient
AllowNoIcons=yes
OutputDir=.\
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
ChangesAssociations=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\WpfMpdClient\bin\release\WpfMpdClient.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\CsUpdater.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\FontAwesome.WPF.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\FontAwesome.WPF.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\LICENSE.TXT"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\mpd_icon.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\Images\next.png"; DestDir: "{app}\Images"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\Images\pause.png"; DestDir: "{app}\Images"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\Images\play.png"; DestDir: "{app}\Images"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\Images\previous.png"; DestDir: "{app}\Images"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\Images\repeat.png"; DestDir: "{app}\Images"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\Images\shuffle.png"; DestDir: "{app}\Images"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\Images\quit.png"; DestDir: "{app}\Images"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\Images\stop.png"; DestDir: "{app}\Images"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\Images\donate.png"; DestDir: "{app}\Images"; Flags: ignoreversion
Source: "..\WpfMpdClient\bin\release\Images\cd.png"; DestDir: "{app}\Images"; Flags: ignoreversion

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\WpfMpdClient"; Filename: "{app}\WpfMpdClient.exe"; WorkingDir: "{app}"
Name: "{group}\{cm:UninstallProgram,WpfMpdClient}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\WpfMpdClient"; Filename: "{app}\WpfMpdClient.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\WpfMpdClient"; Filename: "{app}\WpfMpdClient.exe"; Tasks: quicklaunchicon

[Registry]
Root: HKCR; Subkey: ".prj"; ValueType: string; ValueName: ""; ValueData: "WpfMpdClientProject"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "WpfMpdClientProject"; ValueType: string; ValueName: ""; ValueData: "WpfMpdClient Project"; Flags: uninsdeletekey
Root: HKCR; Subkey: "WpfMpdClient\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\WpfMpdClient.exe,0"
Root: HKCR; Subkey: "WpfMpdClient\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\WpfMpdClient.exe"" ""%1"""

[Run]
Filename: "{app}\WpfMpdClient.exe"; Description: "{cm:LaunchProgram,WpfMpdClient}"; Flags: nowait postinstall skipifsilent

[Code]
function IsDotNetDetected(version: string; service: cardinal): boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1.4322'     .NET Framework 1.1
//    'v2.0.50727'    .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key: string;
    install, serviceCount: cardinal;
    success: boolean;
begin
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + version;
    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;
    // .NET 4.0 uses value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;
    result := success and (install = 1) and (serviceCount >= service);
end;

function InitializeSetup(): Boolean;
begin
    if not IsDotNetDetected('v4\Client', 0) then begin
        MsgBox('{#SetupSetting("AppName")} requires Microsoft .NET Framework 4.0 Client Profile.'#13#13
            'Please use Windows Update to install this version,'#13
            'and then re-run the {#SetupSetting("AppName")} setup program.', mbInformation, MB_OK);
        result := false;
    end else
        result := true;
end;
